plugins {
    id 'com.gladed.androidgitversion' version '0.2.13'
    id "com.jfrog.bintray" version "1.6"

}
apply plugin: 'com.android.application'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        //setProperty("archivesBaseName", "DemoApp-$versionName")
        testBuildType "debug"
        multiDexEnabled true
    }
    buildTypes {
        release {
            //runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
        debug {

        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        quiet false
        abortOnError false
        ignoreWarnings true
        disable 'MissingTranslation' //This is disabled until we get updated translations
    }


}
String versionName = "v-" + System.getenv('TRAVIS_BRANCH') + "-" + System.getenv('TRAVIS_TAG') + "-" + System.getenv('TRAVIS_JOB_NUMBER')

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_APIKEY')
    // override = true // only for plugin version 1.7 which require java8 and still has bugs
    publish = true
    pkg {
        repo = 'AndroidSDKDemoApp'
        name = 'DemoAPK'
        userOrg = 'bluesnap'
        desc = 'BlueSnap Android SDK Demo App'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bluesnap/bluesnap-android-int'
        publicDownloadNumbers = false
        version {
            name = versionName
            released = new Date()
            vcsTag = androidGitVersion.name()
            attributes = [
                    'test'       : String.valueOf(System.getenv('TRAVIS_TEST_RESULT')),
                    'commit'     : String.valueOf(System.getenv('TRAVIS_COMMIT')),
                    'jobnumber'  : String.valueOf(System.getenv('TRAVIS_JOB_NUMBER')),
                    'branch'     : String.valueOf(System.getenv('TRAVIS_BRANCH')),
                    'versioncode': String.valueOf(androidGitVersion.code())
            ]
        }
    }
    filesSpec {
        from('build/outputs/apk') {
            include '*.apk'
            exclude('*.asc')
            exclude('*-debug-unaligned.apk')
            exclude('*-release-unsigned.apk')

        }
        into 'travis/' + System.getenv('TRAVIS_BRANCH') + "/"
        rename { String fileName -> new String('/DemoApp-' + versionName + ".apk")
        }
    }
}

dependencies {
    implementation project(':bluesnap-android')

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    // Added for Google-Pay
    //implementation 'com.android.support:multidex:1.0.3'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'

    androidTestImplementation ('com.android.support.test.espresso:espresso-intents:3.0.2', )

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2')

}

task testSuite(type: Test) {
    include 'PaypalSuite.class'
}

